(ns

    ^{:doc    "Random Utilities, authentication, passwords handling."
      :author "Paweł Wilk"
      :added  "2.0.6"}

    io.randomseed.utils.auth.pwd

  (:import  (java.security SecureRandom)
            (java.util     Arrays))

  (:require [clojure.string                    :as          str]
            [clojure.spec.alpha                :as            s]
            [crypto.equality                   :as       crypto]
            [jsonista.core                     :as         json]
            [io.randomseed.utils.crypto.codecs :as       codecs]
            [io.randomseed.utils.var           :as          var]
            [io.randomseed.utils               :as            u]
            [io.randomseed.utils.map           :as          map]
            [io.randomseed.utils.auth.specs    :refer      :all]
            [io.randomseed.utils.auth.types    :as        types])

  (:import (io.randomseed.utils.auth.types Suites SuitesJSON)))

(defonce ^:private lock 'lock)

(def ^{:dynamic true
       :doc     "Default password authentication timing settings: base wait (seconds),
  random wait range, and extra delay when user does not exist."}
  *default-settings*
  {:wait        1
   :wait-random [0 2]
   :wait-nouser 2})

;;
;; Helper functions
;;

(defn wait
  "Sleeps for `wait-start` seconds plus random delay up to `wait-randmax`."
  [wait-start wait-randmax]
  (s/assert number? wait-start)
  (s/assert number? wait-randmax)
  (when (pos? (+ wait-start wait-randmax))
    (Thread/sleep
     (long
      (* 1000
         (+ wait-start
            (if (pos? wait-randmax) (rand wait-randmax) 0)))))))

(defn salt-bytes
  "Generates cryptographically strong random salt bytes."
  ([] (salt-bytes 16))
  ([size]
   (s/assert pos-int? size)
   (let [sr (java.security.SecureRandom/getInstance "SHA1PRNG")
         buffer (byte-array size)]
     (.nextBytes sr buffer)
     buffer)))

(defn salt-string
  "Generates random salt string of `length` from `possible-chars`."
  (^String [^java.lang.Number length
    ^clojure.lang.PersistentVector possible-chars]
   (s/assert :io.randomseed.utils.auth.settings.cipher/salt-length length)
   (s/assert :io.randomseed.utils.auth.settings.cipher/salt-charset possible-chars)
   (when (and (some? length)
            (some? possible-chars)
            (> length 0)
            (> (count possible-chars) 0))
     (apply str (repeatedly length #(get possible-chars (int (rand (count possible-chars)))))))))

(defn generate-salt
  "Builds salt bytes from random core with optional `prefix` and `suffix`."
  [^java.lang.Number length
   ^clojure.lang.PersistentVector possible-chars
   prefix suffix]
  (s/assert (s/nilable nat-int?) length)
  (s/assert (s/nilable  vector?) possible-chars)
  (when (and (some? length) (> length 0))
    (let [prefix (when (some? prefix) (u/to-bytes prefix))
          suffix (when (some? suffix) (u/to-bytes suffix))
          score  (if (seq possible-chars)
                   (u/to-bytes (salt-string length possible-chars))
                   (salt-bytes length))]
      (u/bytes-concat prefix score suffix))))

(defn standard-check
  "Performs a standard authentication check based on the provided plain text password
  given as the second argument). The first argument should be an encryption function
  used to perform the encryption operation on the provided password and compare the
  result with the encrypted password that should be provided as third argument or as
  a value associated with the :password key if this argument is a map. The last
  argument should be settings map that will be passed to the encryption function.

  This is a low-level function that is intended to be used by different
  authentication modules which are implementing pretty standard way of checking the
  password."
  ([encrypt-fn plain encrypted salt settings]
   (standard-check encrypt-fn plain {:password encrypted :salt salt} settings))
  ([encrypt-fn plain opts encrypted salt settings]
   (standard-check encrypt-fn
                   plain
                   (map/qassoc opts :password encrypted :salt salt)
                   settings))
  ([encrypt-fn plain opts-or-enc settings]
   (s/assert :io.randomseed.utils.auth/encrypt-fn encrypt-fn)
   (s/assert (s/nilable :io.randomseed.utils.auth.settings/generic) settings)
   (s/assert (s/nilable (s/or :pwd-in-map (s/keys :req-un [:io.randomseed.utils.auth.plain/password])
                              :pwd-plain  :io.randomseed.utils.auth.plain/password)) opts-or-enc)
   (let [options   (if (or (nil? opts-or-enc) (map? opts-or-enc)) opts-or-enc {:password opts-or-enc})
         passwd    (get options :password)
         encrypted (when passwd (u/to-bytes passwd))
         ^bytes provided  (get (encrypt-fn plain options settings) :password)]
     (try
       (if (and encrypted provided)
         (crypto/eq? encrypted provided)
         (crypto/eq? (u/to-bytes "@|-.-.-.-.-|@")
                     (u/to-bytes "_ _ ! ! ! _ _")))
       (finally
         (when (bytes? provided) (Arrays/fill provided (byte 0))))))))

(defn find-handler
  "Tries to get an encryption handler from an entry map by accessing :handler key or
  using :handler-id as fallback (and dereferencing it)."
  [password-or-cipher]
  (s/assert (s/nilable (s/or :settings-cipher :io.randomseed.utils.auth.settings/cipher
                             :password-entry  :io.randomseed.utils.auth/password)) password-or-cipher)
  (when (some? password-or-cipher)
    (when-some [h (map/lazy-get password-or-cipher
                                :handler (var/deref (get password-or-cipher :handler-id)))]
      (when (and (map? h) (seq h)) h))))

(defn merge-suites
  "Merges shared and intrinsic suite chains entry-wise.

  Accepts either a `Suites` value or explicit suite chains."
  ([^Suites crypto-suites-dual]
   (s/assert (s/nilable (s/or :settings-suite :io.randomseed.utils.auth.settings.suite/dual
                              :password-chain :io.randomseed.utils.auth.password-chain/dual)) crypto-suites-dual)
   (merge-suites (.shared ^Suites crypto-suites-dual) (.intrinsic ^Suites crypto-suites-dual)))
  ([defaults-crypto-suite user-crypto-suite & more]
   (let [suites (list* defaults-crypto-suite user-crypto-suite more)
         suites (filter identity suites)]
     (s/assert (s/coll-of (s/nilable (s/coll-of (s/or :settings-cipher-entry (s/or :shared :io.randomseed.utils.auth.settings.cipher/shared :intrinsic :io.randomseed.utils.auth.settings.cipher/intrinsic) :password-entry (s/or :shared :io.randomseed.utils.auth.password/shared :intrinsic :io.randomseed.utils.auth.password/intrinsic))))) suites)
     (not-empty (apply map conj suites)))))

;;
;; Encryption handler processor
;;

(defn- entry-handler-wrapper
  [{:keys [^clojure.lang.Symbol  handler
           ^clojure.lang.Keyword name
           ^java.lang.Number salt-length
           ^java.lang.String salt-prefix
           ^java.lang.String salt-suffix
           ^java.lang.String salt-charset]
    :as   opts}                                      ; options for this handler (taken from configuration)
   settings]                                         ; authentication settings
  (locking lock
    (let [handler-id handler                         ; symbolic handler identifier (from options)
          handler    (assoc (var/deref handler-id)   ; actual handler (a map of two functions)
                            :id handler-id)          ; armored by its symbolic identifier (to find it later)
          encrypt-fn (get handler :encrypt-fn)       ; encryption function (from handler)
          check-fn   (get handler :check-fn)         ; checking function (from handler)
          opts       (-> (get handler :defaults)     ; encryption parameters (configuration options merged with handler defaults)
                         (or {}) (conj opts)
                         (map/qupdate :salt-charset vec) ; change string charset to vector (for random access)
                         map/remove-empty-values)
          pub-opts   (dissoc opts                    ; public options (enclosed in an encryption function)
                             :name :encrypt-fn       ; with removed keys which are only needed by this wrapper
                             :check-fn :salt-charset
                             :salt-length :salt-prefix :salt-suffix)]

      ;; add some required values to recalculated configuration options
      (assoc opts

             ;; a map of 2 functions armored by handler's symbolic id and a name
             ;; these functions (for encryption and decryption) are provided by the specific
             ;; authentication module (like scrypt or append).

             :handler handler

             ;; wrapper that encapsulates the original checking function
             ;; it is currently not used since the checker should be able
             ;; to act on a basis of parameters stored along with an encrypted password
             ;; (in order for it to not depend on a current configuration)

             :check-fn
             (fn checker ([plain, pass-opts] (check-fn plain pass-opts settings))
               ([plain, encr, salt] (check-fn plain {:salt salt :password encr} settings))
               ([plain, pass-opts, encr, salt] (check-fn plain (assoc pass-opts :salt salt :password encr) settings)))

             ;; wrapper that encapsulates the original encryption function
             ;; it receives public configuration options of handler (like salt parameters)
             ;; so it's able to use them (like to generate salt) and pass to the original
             ;; encryption function

             :encrypt-fn  (fn encryptor
                            ([plain] (encryptor plain settings))
                            ([plain settings]
                             (encrypt-fn
                              plain
                              (cond-> pub-opts
                                (number? salt-length)
                                (map/qassoc :salt (generate-salt
                                                   salt-length
                                                   (get opts :salt-charset)
                                                   salt-prefix
                                                   salt-suffix)))
                              settings)))))))

;;
;; Public interface
;;

(defn encrypt
  "Encrypts the given plain text password using all encryption functions in the given
  encryption suite."
  [plain local-settings]
  (s/assert :io.randomseed.utils.auth.plain/password plain)
  (let [settings   local-settings
        _          (s/assert (s/nilable :io.randomseed.utils.auth.pwd/settings) settings)
        used-suite (get settings :suite)]
    ;; extract suite from settings or use global as fallback
    ;; loop through all encryption steps in the provided suite
    ;; extracting encryption functions and using them
    ;; on passwords returned by calling previous ones
    ;; then store the results and important parameters (like salt)
    ;; in order (as a vector) while removing :password key from all
    ;; except the last one (which will be needed during checking)
    (loop [lastpass plain, todo used-suite, stack []]
      (let [opts    (first todo)
            encfn   (get opts :encrypt-fn)
            results (if (ifn? encfn)        ; if there is an encryption function
                      (-> lastpass          ; for the previously generated password
                          (encfn settings)  ; run an encryption function on it
                          (map/qassoc           ; to get the encrypted password and a bunch of parameters
                           :handler-id      ; armor the results (a map) with the handler-id
                           (get-in opts     ; containing names of encryption and decryption functions
                                   [:handler :id]))) ; for checking purposes
                      {:password lastpass})
            newpass (get results :password)]
        ;; if there are more items in our chain
        (if-some [nextf (next todo)]
          (recur newpass nextf (conj stack (dissoc results :password))) ; stack the current one and repeat the process
          (conj stack results))))))                                     ; otherwise stack the last results with a final encrypted password

(defn check
  "Checks if the given plain text password is correct by comparing it with the result
  of calling all checkers in the given encryption suite with memorized options
  applied."
  ([plain settings shared-suite intrinsic-suite & other-suites]
   (check plain (apply merge-suites shared-suite intrinsic-suite other-suites) settings))

  ([plain user-suite user-settings]
   (s/assert (s/nilable :io.randomseed.utils.auth.plain/password)   plain)
   (s/assert (s/nilable :io.randomseed.utils.auth/password-chain)   user-suite)
   (s/assert (s/nilable :io.randomseed.utils.auth.settings/generic) user-settings)
   (let [combo?   (instance? Suites user-suite) ; combined settings
         settings user-settings]

     ;; wait some time
     (when-not combo?
       (when-some [waitfn (get settings :wait-fn)] (waitfn user-suite)))

     (if combo? ; if the suite is a Suite then extract :shared and :intrinsic
       (check plain
              (.shared    ^Suites user-suite)
              (.intrinsic ^Suites user-suite)
              settings)

       ;; loop through all entries of a password suite
       ;; trying to resolve encryption handler
       ;; and calling encryption function to recreate hashing process

       (loop [lastpass plain, todo user-suite]
         (let [opts    (or (first todo) {})
               handler (-> opts :handler-id var/deref)
               opts    (-> opts                          ; preparing options for encryption function
                           (dissoc :handler-id)          ; encryption function doesn't have to know that
                           (map/qassoc :checking true))] ; informative flag for the encryption function
           (if-some [nextf (next todo)]
             (let [encrypt-fn (get handler :encrypt-fn)]
               (recur (get (if (ifn? encrypt-fn) (encrypt-fn lastpass opts settings) lastpass) :password) nextf))
             (when-some [checker-fn (get handler :check-fn)]
               (when (ifn? checker-fn) (checker-fn lastpass opts settings))))))))))

;;
;; Encode/decode as JSON
;;

(def ^:private bytes-class
  (class (byte-array 0)))

(defn- bytes->base64-url-safe
  [v]
  (if (instance? bytes-class v)
    (codecs/bin->base64-url-safe v)
    v))

(def ^{:doc "Translation map for JSON serialization: converts byte-array fields to
  URL-safe Base64 strings."}
  json-write-translation
  {:prefix   bytes->base64-url-safe
   :suffix   bytes->base64-url-safe
   :infix    bytes->base64-url-safe
   :salt     bytes->base64-url-safe
   :password bytes->base64-url-safe})

(defn pre-generate-json
  "Prepares suite entry for JSON serialization using translation map `tr-map`."
  [tr-map m]
  (if-not (map? m) m
          (reduce-kv (fn [acc k f]
                       (if (contains? m k)
                         (map/qassoc acc k (f (get m k)))
                         acc))
                     m tr-map)))

(defn to-json
  "Converts the given suite to JSON format."
  ([suite]
   (to-json suite json-write-translation))
  ([suite tr-map]
   (let [suite' (if (sequential? suite)
                  (mapv (partial pre-generate-json tr-map) suite)
                  suite)]
     (json/write-value-as-string suite' json/keyword-keys-object-mapper))))

(defn- base64-any->bin
  "Decodes Base64 payload accepting URL-safe and standard alphabets."
  [v]
  (try
    (codecs/base64-url-safe->bin v)
    (catch Throwable _
      (codecs/base64->bin v))))

(def ^{:doc "Translation map for JSON deserialization: converts Base64-encoded fields back
  to byte arrays and `:handler-id` to a symbol."}
  json-translation
  {:prefix     base64-any->bin
   :suffix     base64-any->bin
   :infix      base64-any->bin
   :salt       base64-any->bin
   :password   base64-any->bin
   :handler-id symbol})

(defn post-parse-json
  "Post-parses JSON data by transforming certain values with the given translation
  map."
  [tr-map m]
  (if-not (map? m) m
          (reduce-kv (fn [acc k f]
                       (if (contains? m k)
                         (map/qassoc acc k (f (get m k)))
                         acc))
                     m tr-map)))

(defn from-json
  "Converts JSON data to suite by applying transformations to keys described by
  tr-map. If no map is given the json-translation is used."
  ([suite]
   (from-json suite json-translation))
  ([suite tr-map]
   (mapv (partial post-parse-json tr-map)
         (json/read-value suite json/keyword-keys-object-mapper))))

;;
;; System suite
;;

(defn printable-suite
  "Returns normalized printable handler names from a suite definition."
  [suite]
  (s/assert (s/nilable :io.randomseed.utils.auth.config/suite) suite)
  (map (comp u/normalize-name :name) suite))

(defn shared
  "Extracts shared part of a single crypto entry."
  [crypto-entry]
  (s/assert (s/nilable :io.randomseed.utils.auth/crypto-entry) crypto-entry)
  ;; try :handler or dereferenced :handler-id to get a handler map
  ;; then try :handler-id of the given entry
  ;; then try :id from the obtained handler map (h)
  ;; then read shared configuration and select shared keys from handler
  (let [h   (find-handler crypto-entry)
        hid (map/lazy-get crypto-entry :handler-id (map/lazy-get h :id (get-in crypto-entry [:handler :id])))]
    (map/qassoc (select-keys crypto-entry (get h :shared)) :handler-id hid)))

(defn shared-suite
  "Extracts shared parts for all entries in `suite`."
  [suite]
  (s/assert :io.randomseed.utils.auth/crypto-suite suite)
  (map shared suite))

(def ^{:doc "Alias for `shared-suite`."} shared-chain shared-suite)

(defn split
  "Splits a cipher entry or a password into two parts and returns a Suite record with
  two fields `:shared` and `:intrinsic` with these parts."
  [crypto-entry]
  (s/assert :io.randomseed.utils.auth/crypto-entry crypto-entry)
  (let [shared-suite (shared crypto-entry)]
    (types/->Suites shared-suite (apply dissoc crypto-entry (keys shared-suite)))))

(defn split-suite
  "Splits all entries in suite into `Suites` with shared/intrinsic chains."
  [suite]
  (s/assert :io.randomseed.utils.auth/crypto-suite suite)
  (loop [todo     suite
         st-chain []
         va-chain []]
    (let [pwd-entry    (first todo)
          nexte        (next  todo)
          shared-pwd   (shared pwd-entry)
          new-st-chain (conj st-chain shared-pwd)
          new-va-chain (conj va-chain (apply dissoc pwd-entry (keys shared-pwd)))]
      (if (nil? nexte)
        (types/->Suites new-st-chain new-va-chain)
        (recur nexte new-st-chain new-va-chain)))))

(def ^{:doc "Alias for `split-suite`."} split-chain split-suite)

(defn human-readable
  "Converts binary fields in a crypto entry to string representation."
  [pwd]
  (s/assert :io.randomseed.utils.auth/crypto-entry pwd)
  (-> pwd
      (map/update-bytes-to-strings :password :prefix :suffix :salt)
      (map/update-existing :name u/normalize-name)))

(defn human-readable-suite
  "Converts every entry in suite to human-readable form."
  [suite]
  (s/assert :io.randomseed.utils.auth/crypto-suite suite)
  (map human-readable suite))

(def ^{:doc "Alias for `human-readable-suite`."} human-readable-chain human-readable-suite)

;;
;; Configuration
;;

(defn prepare-settings
  "Merges provided settings with defaults and removes empty values."
  [config]
  (-> *default-settings*
      (merge config)
      map/remove-empty-values))

(defn- process-handlers
  [handlers settings]
  (map #(entry-handler-wrapper % settings) handlers))

(defn- process-suite
  ([config]
   (process-suite config nil))
  ([config log-fn]
   (when log-fn (log-fn "Registering password encryption suite:" (str/join ", " (printable-suite (:suite config)))))
   (update config :suite process-handlers (dissoc config :suite))))

(defn init-wait
  "Initializes wait function and derived wait configuration values."
  [{:keys [wait-nouser], pwait :wait, [wmin wmax] :wait-random :as config}]
  (s/assert :io.randomseed.utils.auth.config/wait        pwait)
  (s/assert :io.randomseed.utils.auth.config/wait-min    wmin)
  (s/assert :io.randomseed.utils.auth.config/wait-max    wmax)
  (s/assert :io.randomseed.utils.auth.config/wait-nouser wait-nouser)
  (let [znil        (fnil identity 0)
        wait-nouser (znil wait-nouser) ; static delay when user is invalid
        pwait       (znil pwait)       ; static delay
        wmin        (znil wmin)        ; random delay min
        wmax        (znil wmax)        ; random delay max
        wmin        (if (> wmin wmax) wmax wmin)
        wstart      (+ pwait wmin)
        wrandm      (- wmax wmin)]
    (-> config
        (assoc  :wait-fn #(wait (+ wstart (if % 0 wait-nouser)) wrandm))
        (assoc  :wait-config {:wait        pwait
                              :wait-nouser wait-nouser
                              :wait-random wrandm
                              :wait-start  wstart})
        (dissoc :wait :wait-nouser :wait-random))))

(defn new-encryptor
  "Builds encryptor function returning split shared/intrinsic suites."
  [settings]
  (fn password-encrypt
    [plain-text]
    (-> plain-text (encrypt settings) split-suite)))

(defn new-checker
  "Builds checker function for split suites or separate shared/intrinsic chains."
  [settings]
  (fn password-check
    ([password suites]
     (password-check password (.shared ^Suites suites) (.intrinsic ^Suites suites)))
    ([password shared-suite user-suite]
     (if (or shared-suite user-suite)
       (check password (merge-suites shared-suite user-suite) settings)
       (check password nil settings)))))

(defn new-json-encryptor
  "Builds encryptor function returning `SuitesJSON` (shared and intrinsic)."
  [settings]
  (fn password-encrypt-json
    [plain-text]
    (let [suites (-> plain-text (encrypt settings) split-suite)]
      (types/->SuitesJSON (to-json (.shared    ^Suites suites))
                          (to-json (.intrinsic ^Suites suites))))))

(defn new-json-checker
  "Builds checker function for JSON-encoded shared and intrinsic suites."
  [settings]
  (fn password-check-json
    ([password ^SuitesJSON json-suites]
     (password-check-json password
                          (.shared    ^SuitesJSON json-suites)
                          (.intrinsic ^SuitesJSON json-suites)))
    ([password shared-suite-json user-suite-json]
     (if (or shared-suite-json user-suite-json)
       (check password
              (merge-suites (from-json shared-suite-json) (from-json user-suite-json))
              settings)
       (check password nil settings)))))

(defn init
  "Initializes password settings from auth config and optional logger."
  ([k config]
   (init k config nil))
  ([k config log-fn]
   (s/assert :io.randomseed.utils.auth/config config)
   (when log-fn (log-fn "Configuring password authentication:" k))
   (let [config (-> config         ; processing configuration:
                    init-wait      ; initializing delay parameters
                    (process-suite log-fn)
                    (assoc :id k))
         config (assoc config :encrypt-fn      (new-encryptor      config))
         config (assoc config :check-fn        (new-checker        config))
         config (assoc config :encrypt-json-fn (new-json-encryptor config))
         config (assoc config :check-json-fn   (new-json-checker   config))]
     (s/assert :io.randomseed.utils.auth.pwd/settings config)
     config)))

(defn expand-settings
  "Returns prepared settings associated with key `k`."
  [k config]
  {k (prepare-settings config)})
